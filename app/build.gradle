import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileSdkVersion 31

    compileOptions {
        // enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        applicationId "org.zephyrsoft.sdbviewer"
        minSdkVersion 21
        targetSdkVersion 31

        versionCode 7
        versionName "1.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                obfuscate false
                optimizeCode false
            }
        }
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SDBViewer_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation "androidx.fragment:fragment:1.4.1"
    implementation 'com.google.android.material:material:1.6.0'

    def acraVersion = '5.9.3'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation('com.stanfy:gson-xml-java:0.1.7') {
        exclude group: 'xmlpull', module: 'xmlpull'
        constraints {
            implementation('com.google.code.gson:gson:2.9.0') {
                because("there are security problems in older versions")
            }
        }
    }
    //noinspection GradleDependency
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // only necessary for testing, the app will use Android's implementation:
    testImplementation 'xmlpull:xmlpull:1.1.3.1'
    testImplementation 'org.ogce:xpp3:1.1.6'

    testImplementation 'junit:junit:4.13.2'
    def mockitoVersion = "3.12.4" // don't update to 4.x yet because PowerMock doesn't support it
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    def powermockVersion = "2.0.9"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-core:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"

    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}
