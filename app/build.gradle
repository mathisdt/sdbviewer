import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileSdkVersion 33

    compileOptions {
        // enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    namespace 'org.zephyrsoft.sdbviewer'

    defaultConfig {
        applicationId "org.zephyrsoft.sdbviewer"
        minSdkVersion 21
        targetSdkVersion 33

        versionCode 9
        versionName "1.8-SNAPSHOT"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                obfuscate false
                optimizeCode false
            }
        }
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SDBViewer_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation "androidx.fragment:fragment:1.6.1"
    implementation 'com.google.android.material:material:1.9.0'

    def acraVersion = '5.11.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation('com.stanfy:gson-xml-java:0.1.7') {
        exclude group: 'xmlpull', module: 'xmlpull'
        constraints {
            implementation('com.google.code.gson:gson:2.10.1') {
                because("there are security problems in older versions")
            }
        }
    }
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    // only necessary for testing, the app will use Android's implementation:
    testImplementation 'xmlpull:xmlpull:1.1.3.1'
    testImplementation 'org.ogce:xpp3:1.1.6'

    def mockitoVersion = "5.5.0"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}
