import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    compileOptions {
        // enable support for the new language APIs
        coreLibraryDesugaringEnabled = true

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    namespace = 'org.zephyrsoft.sdbviewer'

    defaultConfig {
        applicationId "org.zephyrsoft.sdbviewer"
        minSdkVersion 21
        targetSdk = 36
        compileSdk = 36

        versionCode 11                // increment after every release (for the next development cycle)
        versionName "1.9"    // change with every release

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildFeatures {
        // generate the BuildConfig class
        buildConfig = true
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode = false
                obfuscate = false
                optimizeCode = false
            }
        }
    }

    // rename final APK files
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "SDBViewer_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.3.0"
    implementation "androidx.fragment:fragment:1.8.9"
    implementation 'com.google.android.material:material:1.12.0'

    def acraVersion = '5.12.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation('com.stanfy:gson-xml-java:0.1.7') {
        exclude group: 'xmlpull', module: 'xmlpull'
        constraints {
            implementation('com.google.code.gson:gson:2.13.1') {
                because("there are security problems in older versions")
            }
        }
    }
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // only necessary for testing, the app will use Android's implementation:
    testImplementation 'xmlpull:xmlpull:1.1.3.1'
    testImplementation 'org.ogce:xpp3:1.1.6'

    def mockitoVersion = "5.19.0"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}
