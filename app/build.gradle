import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd-HH-mm-ss")
    df.setTimeZone(TimeZone.getTimeZone("Europe/Berlin"))
    return df.format(new Date())
}

android {
    // the same SDK has to be configured in .travis.yml:
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "org.zephyrsoft.sdbviewer"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 7
        versionName "1.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    // rename final APK files
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SDBViewer_${variant.versionName}_${buildTime()}_${variant.buildType.name}${variant.signingReady ? "" : "-unsigned"}.apk"
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    def acraVersion = '5.2.1'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation('com.stanfy:gson-xml-java:0.1.7') {
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    //noinspection GradleDependency
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.20'

    // only necessary for testing, the app will use Android's implementation:
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'xmlpull:xmlpull:1.1.3.1'
    testImplementation 'org.ogce:xpp3:1.1.6'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    def powermockVersion = "2.0.7"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-core:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"

    androidTestImplementation 'org.mockito:mockito-android:3.3.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
